import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    id("com.android.application")
    id("org.jetbrains.kotlin.android")
}

android {
    compileSdk = 36

    defaultConfig {
        applicationId = "com.jpaver.myapplication"
        minSdk = 26
        targetSdk = 35
        versionCode = 1348
        versionName = "7.60"
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled = true
    }

    buildTypes {
        getByName("release") {
            isMinifyEnabled = false
            proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro")
            signingConfig = signingConfigs.getByName("debug")
        }
    }
    
    flavorDimensions.add("mode")

    productFlavors {
        create("dev") {
            dimension = "mode"
            applicationId = "com.jpaver.myapplication"
            targetSdk = 35
        }
        create("free") {
            dimension = "mode"
            applicationId = "com.jpaver.myapplication"
            targetSdk = 35
        }
        create("full") {
            dimension = "mode"
            applicationId = "com.paver.myapplication"
            targetSdk = 35
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlin {
        compilerOptions {
            jvmTarget.set(JvmTarget.JVM_17)
        }
    }

    namespace = "com.jpaver.trianglelist"
    
    buildFeatures {
        buildConfig = true
        viewBinding = true
    }
    
    lint {
        // baseline = file("lint-baseline.xml")  // ä¸€æ™‚çš„ã«ç„¡åŠ¹åŒ–ã—ã¦å…¨è­¦å‘Šã‚’ç¢ºèª
        abortOnError = false  // è­¦å‘Šèª¿æŸ»ã®ãŸã‚ä¸€æ™‚çš„ã«false
        warningsAsErrors = false
    }
}

dependencies {
    implementation(kotlin("reflect"))
    implementation(kotlin("stdlib-jdk8"))
    implementation("androidx.appcompat:appcompat:1.7.1")
    implementation("androidx.activity:activity-ktx:1.10.1")
    implementation("androidx.navigation:navigation-fragment-ktx:2.9.3")
    implementation("androidx.navigation:navigation-ui-ktx:2.9.3")
    implementation("androidx.constraintlayout:constraintlayout:2.2.1")
    implementation("com.google.android.material:material:1.12.0")
    implementation("com.google.android.gms:play-services-ads:24.5.0")
    implementation("androidx.preference:preference-ktx:1.2.1")
    implementation(fileTree(mapOf("dir" to "libs", "include" to listOf("*.aar", "*.jar"))))
    implementation("org.apache.poi:poi-ooxml:5.4.1")
    testImplementation("junit:junit:4.13.2")
    testImplementation("io.mockk:mockk:1.14.5")
    testImplementation("org.powermock:powermock-module-junit4:2.0.9")
    testImplementation("org.powermock:powermock-api-mockito:1.7.4")
    testImplementation("org.mockito:mockito-core:5.18.0")
    testImplementation("org.mockito:mockito-inline:5.2.0")
    testImplementation("org.robolectric:robolectric:4.15.1")
    implementation(project(":common"))
    // implementation(project(":core"))  // ä¸€æ™‚çš„ã«ã‚³ãƒ¡ãƒ³ãƒˆã‚¢ã‚¦ãƒˆ (lintè­¦å‘Šç¢ºèªç”¨)
    testImplementation(project(":common"))
    androidTestImplementation("androidx.test:rules:1.7.0")
    androidTestImplementation("androidx.test.ext:junit:1.3.0")
    androidTestImplementation("androidx.test.espresso:espresso-core:3.7.0")
}

// ã‚«ã‚¹ã‚¿ãƒ ã‚¿ã‚¹ã‚¯: ã‚³ãƒŸãƒƒãƒˆå‰ãƒã‚§ãƒƒã‚¯
tasks.register("preCommitChecks") {
    group = "verification"
    description = "ã‚³ãƒŸãƒƒãƒˆå‰ã®å…¨ãƒã‚§ãƒƒã‚¯ã‚’å®Ÿè¡Œ"
    dependsOn("lintDevDebug", "testDevDebugUnitTest")
    
    doLast {
        println("âœ… å…¨ã¦ã®pre-commitãƒã‚§ãƒƒã‚¯ãŒå®Œäº†ã—ã¾ã—ãŸï¼")
    }
}

// ã‚«ã‚¹ã‚¿ãƒ ã‚¿ã‚¹ã‚¯: ãƒªãƒªãƒ¼ã‚¹æº–å‚™
tasks.register("prepareRelease") {
    group = "release"
    description = "ãƒªãƒªãƒ¼ã‚¹æº–å‚™: versionCodeè‡ªå‹•æ›´æ–°"
    notCompatibleWithConfigurationCache("Modifies version fields in build script at execution time")
    
    doLast {
        val buildFile = file("build.gradle.kts")
        val content = buildFile.readText()
        
        // ç¾åœ¨ã®ãƒãƒ¼ã‚¸ãƒ§ãƒ³å–å¾—
        val currentVersionCode = Regex("versionCode = (\\d+)").find(content)?.groupValues?.get(1)?.toInt()
        val currentVersionName = Regex("versionName = \"([^\"]+)\"").find(content)?.groupValues?.get(1)
        
        println("ğŸ“± ç¾åœ¨ã®ãƒãƒ¼ã‚¸ãƒ§ãƒ³:")
        println("   versionCode: $currentVersionCode")
        println("   versionName: $currentVersionName")
        println("")
        
        if (currentVersionCode != null) {
            val newVersionCode = currentVersionCode + 1
            
            // versionCodeã®ã¿è‡ªå‹•æ›´æ–°
            val newContent = content.replace(
                Regex("versionCode = \\d+"),
                "versionCode = $newVersionCode"
            )
            buildFile.writeText(newContent)
            
            println("ğŸ”„ versionCode ã‚’ $currentVersionCode â†’ $newVersionCode ã«æ›´æ–°ã—ã¾ã—ãŸ")
            println("âš ï¸  versionName ã¯æ‰‹å‹•ã§ç¢ºèªãƒ»å¤‰æ›´ã—ã¦ãã ã•ã„")
            println("")
            println("æ¬¡ã®æ‰‹é †:")
            println("1. versionName ã‚’æ‰‹å‹•ã§æ›´æ–°")
            println("2. ./gradlew assembleRelease ã§ãƒ“ãƒ«ãƒ‰")
        } else {
            println("âŒ versionCode ã®å–å¾—ã«å¤±æ•—ã—ã¾ã—ãŸ")
        }
    }
}
